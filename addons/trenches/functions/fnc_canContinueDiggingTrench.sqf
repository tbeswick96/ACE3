#include "script_component.hpp"
/*
 * Author: SzwedzikPL
 * Checks if a unit can continue digging a trench
 *
 * Arguments:
 * 0: trench <OBJECT>
 * 1: unit <OBJECT>
 *
 * Return Value:
 * Can continue <BOOL>
 *
 * Example:
 * [TrenchObj, ACE_player] call ace_trenches_fnc_canContinueDiggingTrench
 *
 * Public: No
 */

params ["_trench", "_unit"];

if !(GVAR(allowDigging)) exitWith {false};
if (GVAR(digRequireEntrenchmentTool) && {!("ACE_EntrenchingTool" in (_unit call EFUNC(common,uniqueItems)))}) exitWith {false};
if ((_trench getVariable [QGVAR(progress), 1]) >= 1) exitWith {false};

// Prevent removing/digging trench by more than one person
if (_trench getVariable [QGVAR(digging), false]) exitWith {false};

true
